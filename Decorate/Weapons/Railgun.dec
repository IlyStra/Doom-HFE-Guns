Actor HFEG_RailGun1 : Weapon
{
	Weapon.UpSound "GUNDRA"
	Weapon.AmmoUse 15
	Weapon.AmmoType "Cell"
	Weapon.SlotNumber 6
	Weapon.AmmoGive 15
	Scale 0.75
	Obituary "%o was railed by %k`s railgun."
	Inventory.Pickupmessage "You got the UAC Railgun!"
	Tag "UAC ''IA-RG Prototype V 4'' Railgun"
	States
	{
	Ready:
     TNT1 A 0 A_JumpIfInventory("Meleeing",1,"FastMeleeBase")
     TNT1 A 0 A_JumpIfInventory("Grenading",1,"FastGrenadeBase")
		TNT1 A 0 Bright A_JumpIfInventory("UpgradeToken", 1, "Upgrade")
		RLG0 I 1 Bright A_WeaponReady(WRF_AllowZoom)
		Loop
	Upgrade:
		TNT1 I 0 Bright A_PlaySound("WEAPUP",64)
		TNT1 I 1 Bright A_Print("Upgraded to level 2!\n\nAccuracy+ - Scope+",5,"SMALLFONT")
		TNT1 I 0 Bright A_GiveInventory("HFEG_RailGun2")
		TNT1 I 0 Bright A_TakeInventory("UpgradeToken",9)
		TNT1 I 0 Bright A_SelectWeapon ("HFEG_RailGun2")
		TNT1 I 0 Bright A_TakeInventory("HFEG_RailGun1")
		TNT1 I 1 Bright
	Goto Ready
	Deselect:
		RLG0 I 1 Bright Offset(0, 34)
		RLG0 I 1 Bright Offset(0, 47)
		RLG0 I 1 Bright Offset(0, 69)
		RLG0 I 1 Bright Offset(0, 100)
		RLG0 I 1 Bright A_Lower
		Wait
	Select:
		RLG0 I 1 Bright Offset(0, 100)
		RLG0 I 1 Bright Offset(0, 69)
		RLG0 I 1 Bright Offset(0, 47)
		RLG0 I 1 Bright Offset(0, 34)
		RLG0 I 1 Bright A_Raise
		Wait
	Fire:
		RLG0 I 0 A_PlaySound("RailGun_Charge", Chan_Weapon)
		RLG0 HGFABC 4 Bright
	FireLoop:
		RLG0 C 0 Bright A_PlaySound("GLEXP", Chan_Weapon)
		RPP7 BBBBBBBB 0 A_FireCustomMissile("HFEG_RailgunLaunchFlame", FRandom(-16, 16), False, 8, 0, 0, FRandom(-16, 16))
		RPP7 BBBB 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-16, 16), False, 8, 0, 0, FRandom(-16, 16))
		TNT1 C 0 Bright A_FireCustomMissile("HFEG_RailgunProjL1")
		TNT1 C 0 Bright A_SetPitch(pitch-2)
		TNT1 C 0 Bright A_Zoomfactor(0.98)
		RLG0 D 1 Bright Offset(2, 35)
		RLG0 E 1 Bright Bright Offset(4, 40)
		RLG0 C 1 Bright Offset(6, 43)
		TNT1 C 0 Bright A_Zoomfactor(1.0)
		RLG0 C 1 Bright Offset(8, 45)
		RLG0 C 1 Bright Offset(9, 49)
		RLG0 C 1 Bright Offset(10, 50)
		RLG0 C 1 Bright Offset(9, 49)
		RLG0 C 1 Bright Offset(7, 45)
		RLG0 C 1 Bright Offset(5, 43)
		RLG0 C 1 Bright Offset(3, 40)
		RLG0 C 1 Bright Offset(1, 38)
		RLG0 C 1 Bright Offset(0, 36)
		RLG0 C 1 Bright Offset(0, 34)
		RLG0 C 1 Bright A_Refire("FireLoop")
		RLG0 CBAFGHI 4 Bright
		Goto Ready
	Spawn:
		M72W K -1
		Stop
	}
}

Actor HFEG_RailgunLaunchFlame
{
	Radius 21
	Height 42
	Speed 24
	BounceFactor 1
	WallBounceFactor 1
	RenderStyle Add
	+NoBlockmap
	+Missile
	+Bright
	+NoGravity
	+BounceOnWalls
	+BounceOnFloors
	+BounceOnCeilings
	+BounceOnActors
	States
	{
	Spawn:
		CRF3 A Random(0, 4) A_ScaleVelocity(0.75)
		CRF3 ABCDEFGHIJKLMNOP 1 A_ScaleVelocity(0.75)
		Stop
	}
}

Actor HFEG_RailgunProjL1 : FastProjectile
{
	Damage (0)
	Speed 512
	RenderStyle None
	Radius 1
	Height 1
	States
	{
	Spawn:
		TNT1 A 1 A_SpawnItemEx("HFEG_RailgunTrailL1Light", (1 *VelX)/-35.0, -(1 *VelY)/-35.0, (1 *VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1", (8 *VelX)/-35.0, -(8 *VelY)/-35.0, (8 *VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1Light", (16 *VelX)/-35.0, -(16 *VelY)/-35.0, (16 *VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1", (24*VelX)/-35.0, -(24 *VelY)/-35.0, (24 *VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1Light", (32 *VelX)/-35.0, -(32 *VelY)/-35.0, (32 *VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1", (48*VelX)/-35.0, -(48*VelY)/-35.0, (48*VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1Light", (56*VelX)/-35.0, -(56*VelY)/-35.0, (56*VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		TNT1 A 0 A_SpawnItemEx("HFEG_RailgunTrailL1", (64*VelX)/-35.0, -(64*VelY)/-35.0, (64*VelZ)/-35.0, 0,0,0, 0, SXF_AbsoluteAngle)
		Loop
	Death:
		TNT1 A 0 A_Explode((256), 256, 0, False, 60)
		TNT1 A 0 Radius_Quake(2, 4, 0, 7, 0)
		TNT1 AAAA 0 A_SpawnItemEx("HFEG_RailL1Flame", 0, 0, 0, FRandom(0.1, 1.0), 0, FRandom(-1.0, 1.0), Random(0, 360), SXF_MultiplySpeed)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("HFEG_ExplodeStyle") == 1, "Optimized")
		TNT1 AAAAAAAA 0 A_SpawnItemEx("HFEG_RailL1Flame", 0, 0, 0, FRandom(0.1, 1.0), 0, FRandom(-1.0, 1.0), Random(0, 360), SXF_MultiplySpeed)
		TNT1 AAAA 0 A_SpawnItemEx("HFEG_RExplodeSmoke1", 0, 0, 0, FRandom(1, 2.0), 0, FRandom(-2.0, 2.0), Random(0, 360), SXF_MultiplySpeed)
		TNT1 AAAA 0 A_SpawnItemEx("HFEG_RExplodeSmoke1", 0, 0, 0, FRandom(1, 2.0), 0, FRandom(-2.0, 2.0), 180+Random(-90, 90), SXF_MultiplySpeed)
		TNT1 A 0 A_SpawnItemEx("UACHandGrenadeShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	Optimized:
		TNT1 A 0 A_PlaySound("misc/boom", Chan_6)
		TNT1 A 0 A_PlaySound("misc/distanceboom", Chan_5, 1.0, False, 0.375)
		TNT1 A 140
		Stop
	}
}

Actor HFEG_RailL1Flame
{
	Radius 21
	Height 42
	Speed 12
	BounceFactor 1
	WallBounceFactor 1
	RenderStyle Add
	+NoBlockmap
	+Missile
	+DontSplash
	+Bright
	+NoGravity
	+BounceOnWalls
	+BounceOnFloors
	+BounceOnCeilings
	+BounceOnActors
	States
	{
	Spawn://Lots of tabulation on TNT1A0 functions to better distinguish between secondary functions
		CRF3 A Random(0, 4) A_ScaleVelocity(0.75)
		CRF3 B 1 A_Explode((Random(0, 1)), 56, 0, False, 32)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 C 1 A_Explode((Random(0, 1)), 64, 0, False, 48)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 D 1 A_Explode((Random(0, 1)), 56, 0, False, 56)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 E 1 A_Explode((Random(0, 1)), 64, 0, False, 48)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 F 1 A_Explode((Random(0, 1)), 56, 0, False, 56)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 G 1 A_Explode((Random(0, 1)), 64, 0, False, 48)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 H 1 A_Explode((Random(0, 1)), 56, 0, False, 56)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 I 1 A_Explode((Random(0, 1)), 64, 0, False, 56)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 J 1 A_Explode((Random(0, 1)), 56, 0, False, 56)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 K 1 A_Explode((Random(0, 1)), 65, 0, False, 48)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 L 1 A_Explode((Random(0, 1)), 56, 0, False, 32)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 M 1 A_Explode((Random(0, 1)), 48, 0, False, 24)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 N 1 A_Explode((Random(0, 1)), 32, 0, False, 18)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 O 1 A_Explode((Random(0, 1)), 24, 0, False, 16)
															TNT1 A 0 A_ScaleVelocity(0.75)
		CRF3 P 1 A_Explode((Random(0, 1)), 16, 0, False, 12)
		Stop
	}
}

Actor HFEG_RailgunTrailL1Light
{
	+NoInteraction +ClientSideOnly
	Scale 0.25
	RenderStyle Add
	+Bright
	States
	{
	Spawn:
		CRF3 ABCDEFGHIJKLMNOP 1 Light("PLASMABALL")
		Stop
	}
}

Actor HFEG_RailgunTrailL1
{
	+NoInteraction +ClientSideOnly
	Scale 0.25
	RenderStyle Add
	+Bright
	States
	{
	Spawn:
		CRF3 ABCDEFGHIJKLMNOP 1
		Stop
	}
}
