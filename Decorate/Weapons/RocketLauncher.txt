Actor HFEG_RkLauncherL1 : UACweapon
{
	Weapon.UpSound "GUNDRA"
	Weapon.AmmoUse 1
	Weapon.AmmoType "HFEG_OneRocketBox"
	Weapon.SlotNumber 5
	Weapon.AmmoGive 4
	Scale 0.625
	Obituary "%o was homed by %k`s HELRL."
	Inventory.Pickupmessage "You got the HELRL!"
	Tag "UAC ''HELRL''"
	States
	{
	Ready:
     TNT1 A 0 A_JumpIfInventory("Meleeing",1,"FastMeleeBase")
     TNT1 A 0 A_JumpIfInventory("Grenading",1,"FastGrenadeBase")
		TNT1 A 0 A_JumpIfInventory("UpgradeToken", 1, "Upgrade")
		HRL0 A 1 A_WeaponReady(WRF_AllowZoom)
		Loop
	Upgrade:
		TNT1 A 0 A_PlaySound("WEAPUP",64)
		TNT1 A 1 A_Print("Upgraded to level 2!\n\nHOMING- DAMAGE+ - CUMUNILATIVE JET LENGHT+ - ACCURACY+",5,"SMALLFONT")
		TNT1 A 0 A_GiveInventory("HFEG_RkLauncherL2")
		TNT1 A 0 A_TakeInventory("UpgradeToken",9)
		TNT1 A 0 A_SelectWeapon ("HFEG_RkLauncherL2")
		TNT1 A 0 A_TakeInventory("HFEG_RkLauncherL1")
		TNT1 A 1
	Goto Ready
	Deselect:
		HRL0 A 1 Offset(0, 34)
		HRL0 A 1 Offset(0, 47)
		HRL0 A 1 Offset(0, 69)
		HRL0 A 1 Offset(0, 100)
		HRL0 A 1 A_Lower
		Wait
	Select:
		HRL0 A 1 Offset(0, 100)
		HRL0 A 1 Offset(0, 69)
		HRL0 A 1 Offset(0, 47)
		HRL0 A 1 Offset(0, 34)
		HRL0 A 1 A_Raise
		Wait
	Fire:
		HRL0 A 0 A_PlaySound("RocketL1WLaunch", Chan_Weapon)
		HRL0 A 0 A_FireCustomMissile("HFEG_CRocketL1", 0, True, 4)
		HRL0 B 1
		HRL0 BBBBBBBB 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-6, 6), False, 4, 0, 0, FRandom(-6, 6))
		HRL0 BC 2
		HRL0 C 1 Offset(2, 35)
		HRL0 C 1 Offset(4, 39)
		HRL0 C 1 Offset(5, 43)
		HRL0 C 1 Offset(4, 39)
		HRL0 C 1 Offset(2, 35)
		HRL0 C 0 Offset(0, 32)
		HRL0 D 0 A_PlaySound("RocketL1WReload", Chan_Body)
		HRL0 DEF 2
		HRL0 A 1 A_Refire 
		Goto Ready
	Spawn:
		HRL0 G -1
		Stop
	}
}

Actor HFEG_RCL1Smoke0
{
	+NoInteraction
	+ClientSideOnly
	Scale 0.0
	Alpha 1.0
	Speed 32
	RenderStyle Translucent
	States
	{
	Spawn:
		CRS0 A 0
		CRS0 A 0 A_ScaleVelocity(FRandom(0.125, 1.5))
		CRS0 A 0 A_Jump(72, "ExplodeStart")
		CRS0 B 0 A_Jump(72, "ExplodeStart")
		CRS0 C 0
	ExplodeStart:
		"####" "#" 1 A_SetScale(ScaleX+0.1, ScaleX+0.1)
		"####" "#" 0 A_ScaleVelocity(0.875)
		"####" "#" 0 A_SetTranslucent(Alpha-0.0625)
		"####" "#" 0 A_JumpIf(Alpha<=0.75, "ExplodeFadeOut")
		Loop
	ExplodeFadeOut:
		"####" "#" 1 A_SetScale(ScaleX+0.01875, ScaleX+0.01875)
		"####" "#" 0 A_ScaleVelocity(0.875)
		"####" "#" 0 A_SetTranslucent(Alpha-0.0375)
		"####" "#" 0 A_JumpIf(Alpha<=0.0, "Null")
		Loop
	}
}

Actor RocketLauncherSpawner : CustomInventory
{
	Inventory.Icon "TNT1A0"
	Inventory.PickupSound "GUNPI"
	Inventory.PickupMessage "You got the rocket launcher!"
	Scale 0.625
	States
	{
	Spawn:
		HRL0 G -1
		Stop
	Pickup:
	    TNT1 A 0 A_JumpIfInventory("HFEG_RkLauncherL3", 1, "Ammo")
		TNT1 A 0 A_JumpIfInventory("HFEG_RkLauncherL2", 1, "Ammo")
		TNT1 A 0 A_JumpIfInventory("HFEG_RkLauncherL1", 1, "Ammo")
		TNT1 A 0 A_GiveInventory("HFEG_RkLauncherL1")
		Stop
	Ammo:
		TNT1 A 0 A_GiveInventory("HFEG_OneRocketBox", 1)
		Stop
	}
}

Actor HFEG_RkLauncherL2 : UACweapon
{
	+CheatNotWeapon
	Weapon.UpSound "GUNDRA"
	Weapon.AmmoUse 1
	Weapon.AmmoType "HFEG_OneRocketBox"
	Weapon.SlotNumber 5
	Scale 0.625
	Weapon.BobRangeY 0.625
	Weapon.BobRangeX 0.375
	Obituary "%o was killed by %k`s UAC Commando X-17."
	Inventory.Pickupmessage "You got the UAC Commando X-17!"
	Tag "''UAC Commando X-17'' Warhead: Standart PG-7"
	States
	{
	Ready:
     TNT1 A 0 A_JumpIfInventory("Meleeing",1,"FastMeleeBase")
     TNT1 A 0 A_JumpIfInventory("Grenading",1,"FastGrenadeBase")
		TNT1 A 0 A_JumpIfInventory("UpgradeToken", 1, "Upgrade")
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, 3)
		MRX7 A 1 A_WeaponReady(WRF_AllowZoom)
		MRX7 A 1 A_Jump(256, "Ready")
		SNIP A 1 A_WeaponReady(WRF_AllowZoom)
		Loop
	Upgrade:
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "UpgradeZoomed")
		TNT1 A 0 A_PlaySound("WEAPUP",64)
		TNT1 A 1 A_Print("Upgraded to level 3!\n\nDAMAGE+ - CUMUNILATIVE JET LENGHT+ - ACCURACY+ - EXPLODE SIZE+",5,"SMALLFONT")
		TNT1 A 0 A_GiveInventory("HFEG_RkLauncherL3")
		TNT1 A 0 A_TakeInventory("UpgradeToken",9)
		TNT1 A 0 A_SelectWeapon ("HFEG_RkLauncherL3")
		TNT1 A 0 A_TakeInventory("HFEG_RkLauncherL2")
		TNT1 A 1
	Goto Ready
	UpgradeZoomed:
		TNT1 A 0 A_PlaySound("WEAPUP",64)
		TNT1 A 1 A_Print("Upgraded to level 3!\n\nDAMAGE+ - CUMUNILATIVE JET LENGHT+ - ACCURACY+ - EXPLODE SIZE+",5,"SMALLFONT")
		TNT1 A 0 A_GiveInventory("HFEG_RkLauncherL3")
		TNT1 A 0 A_TakeInventory("UpgradeToken",9)
		TNT1 A 0 A_SelectWeapon ("HFEG_RkLauncherL3")
		TNT1 A 0 A_TakeInventory("HFEG_RkLauncherL2")
		TNT1 A 1
	Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "UnZoomAndDeselect")
		MRX7 A 1 Offset(0, 34)
		MRX7 A 1 Offset(0, 47)
		MRX7 A 1 Offset(0, 69)
		MRX7 A 1 Offset(0, 100)
		MRX7 A 1 A_Lower
		Wait
	Select:
		MRX7 A 1 Offset(0, 100)
		MRX7 A 1 Offset(0, 69)
		MRX7 A 1 Offset(0, 47)
		MRX7 A 1 Offset(0, 34)
		MRX7 A 1 A_Raise
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "ZoomSelect")
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "FireZoomed")
		MRX7 A 0 A_PlaySound("RocketL2WLaunch", Chan_Weapon)
		MRX7 A 0 A_FireCustomMissile("HFEG_CRocketL2", 0, True, 4)
		MRX7 A 0
		MRX7 AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", 180+FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 A 1 Offset(3, 37)
		MRX7 A 1 Offset(5, 38)
		MRX7 A 1 Offset(9, 40)
		MRX7 A 1 Offset(12, 43)
		MRX7 A 1 Offset(13, 45)
		MRX7 A 1 Offset(12, 43)
		MRX7 A 1 Offset(9, 40)
		MRX7 A 1 Offset(5, 38)
		MRX7 A 1 Offset(3, 37)
		MRX7 A 1 Offset(1, 35)
		MRX7 A 1 Offset(0, 33)
		MRX7 A 0 A_PlaySound("RocketL1WReload", Chan_Body)
		MRX7 A 6
		MRX7 A 1 A_Refire
		Goto Ready
	FireZoomed:
		SNIP A 0 A_PlaySound("RocketL2WLaunch", Chan_Weapon)
		SNIP A 0 A_FireCustomMissile("HFEG_CRocketL2", 0, True, 0)
		SNIP A 4 A_ZoomFactor(0.875)
		SNIP AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", FRandom(-16, 16), False, 0, 0, 0, FRandom(-16, 16))
		SNIP AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-16, 16), False, 0, 0, 0, FRandom(-16, 16))
		SNIP A 4 A_ZoomFactor(1.25)
		SNIP A 11
		SNIP A 0 A_PlaySound("RocketL1WReload", Chan_Body)
		SNIP A 6
		Goto Ready
	Spawn:
		MRX7 B -1
		Stop
	AltFire:
		TNT1 A 0 A_PlayWeaponsound("ZOOMTIC")
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "UnZoom")
		MRX7 A 0 A_ZoomFactor(1.25)
		MRX7 A 0 A_GiveInventory("HFEG_RCL2Zoom", 1)
		Goto Ready
	ZoomSelect:
		MRX7 A 0 A_ZoomFactor(1.25)
		Wait
	UnZoom:
		MRX7 A 0 A_ZoomFactor(1.0)
		MRX7 A 0 A_TakeInventory("HFEG_RCL2Zoom", 1)
		Goto Ready
	UnZoomAndDeselect:
		MRX7 A 0 A_ZoomFactor(1.0)
		Goto Deselect+1
	DeselectNoCheck:
		Goto Deselect+1
	}
}

Actor HFEG_RCL2Zoom : Inventory {Inventory.MaxAmount 1 +IGNORESKILL}

Actor HFEG_RCL2LaunchFlame
{
	Radius 21
	Height 42
	Speed 24
	BounceFactor 1
	WallBounceFactor 1
	RenderStyle Add
	+NoBlockmap
	+Missile
	+Bright
	+NoGravity
	+BounceOnWalls
	+BounceOnFloors
	+BounceOnCeilings
	+BounceOnActors
	States
	{
	Spawn:
		CRF7 A Random(0, 4) A_ScaleVelocity(0.75)
		CRF7 ABCDEFGHIJKLMNOP 1 A_ScaleVelocity(0.75)
		Stop
	}
}

Actor HFEG_RkLauncherL3 : UACweapon
{
	+CheatNotWeapon
	Weapon.UpSound "GUNDRA"
	Weapon.AmmoUse 1
	Weapon.AmmoType "HFEG_OneRocketBox"
	Weapon.SlotNumber 5
	Scale 0.625
	Weapon.BobRangeY 0.625
	Weapon.BobRangeX 0.375
	Obituary "%o was killed by %k`s UAC Commando X-17."
	Inventory.Pickupmessage "You got the UAC Commando X-17!"
	Tag "''UAC Commando X-17'' Warhead: TBG-7"
	States
	{
	Upgrade:
		TNT1 A 1 A_Print("THE WEAPON IS ALREADY UPGRADED TO THE MAX LEVEL!",5,"SMALLFONT")
		TNT1 A 0 A_GiveInventory("UACTOOLZ", 1)
		TNT1 A 0 A_TakeInventory("UpgradeToken",9)
		TNT1 A 1
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "FireZoomed")
		MRX7 A 0 A_PlaySound("RocketL3WLaunch", Chan_Weapon)
		MRX7 A 0 A_FireCustomMissile("HFEG_CRocketL3", 0, True, 4)
		MRX7 A 0 A_ZoomFactor(0.75)
		MRX7 A 0 A_SetPitch(Pitch-6)
		MRX7 A 0 A_SetAngle(Angle-6)
		MRX7 AAAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 AAAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", 180+FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 AAAAAAAAA 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-16, 16), False, 4, 0, 0, FRandom(-16, 16))
		MRX7 A 1 Offset(3, 37)
		MRX7 A 1 Offset(5, 38)
		MRX7 A 1 Offset(9, 40)
		MRX7 A 1 Offset(12, 43)
		MRX7 A 1 Offset(13, 45)
		MRX7 A 0 A_ZoomFactor(1)
		MRX7 A 1 Offset(12, 43)
		MRX7 A 1 Offset(9, 40)
		MRX7 A 1 Offset(5, 38)
		MRX7 A 1 Offset(3, 37)
		MRX7 A 1 Offset(1, 35)
		MRX7 A 1 Offset(0, 33)
		MRX7 A 0 A_PlaySound("RocketL1WReload", Chan_Body)
		MRX7 A 6
		MRX7 A 1 A_Refire 
		Goto Ready
	FireZoomed:
		SNIP A 0 A_PlaySound("RocketL2WLaunch", Chan_Weapon)
		SNIP A 0 A_FireCustomMissile("HFEG_CRocketL2", 0, True, 0)
		SNIP A 4 A_ZoomFactor(0.75)
		SNIP AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL2LaunchFlame", FRandom(-16, 16), False, 0, 0, 0, FRandom(-16, 16))
		SNIP AAAAAAAA 0 A_FireCustomMissile("HFEG_RCL1Smoke0", FRandom(-16, 16), False, 0, 0, 0, FRandom(-16, 16))
		SNIP A 4 A_ZoomFactor(1.25)
		SNIP A 11
		SNIP A 0 A_PlaySound("RocketL1WReload", Chan_Body)
		SNIP A 6
		SNIP A 1 A_Refire 
		Goto Ready
	Ready:
     TNT1 A 0 A_JumpIfInventory("Meleeing",1,"FastMeleeBase")
     TNT1 A 0 A_JumpIfInventory("Grenading",1,"FastGrenadeBase")
		TNT1 A 0 A_JumpIfInventory("UpgradeToken", 1, "Upgrade")
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, 3)
		MRX7 A 1 A_WeaponReady(WRF_AllowZoom)
		MRX7 A 1 A_Jump(256, "Ready")
		SNIP A 1 A_WeaponReady(WRF_AllowZoom)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "UnZoomAndDeselect")
		MRX7 A 1 Offset(0, 34)
		MRX7 A 1 Offset(0, 47)
		MRX7 A 1 Offset(0, 69)
		MRX7 A 1 Offset(0, 100)
		MRX7 A 1 A_Lower
		Wait
	Select:
		MRX7 A 1 Offset(0, 100)
		MRX7 A 1 Offset(0, 69)
		MRX7 A 1 Offset(0, 47)
		MRX7 A 1 Offset(0, 34)
		MRX7 A 1 A_Raise
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "ZoomSelect")
		Wait
	Spawn:
		MRX7 B -1
		Stop
	AltFire:
		TNT1 A 0 A_PlayWeaponsound("ZOOMTIC")
		TNT1 A 0 A_JumpIfInventory("HFEG_RCL2Zoom", 1, "UnZoom")
		MRX7 A 0 A_ZoomFactor(1.25)
		MRX7 A 0 A_GiveInventory("HFEG_RCL2Zoom", 1)
		Goto Ready
	ZoomSelect:
		MRX7 A 0 A_ZoomFactor(1.25)
		Wait
	UnZoom:
		MRX7 A 0 A_ZoomFactor(1.0)
		MRX7 A 0 A_TakeInventory("HFEG_RCL2Zoom", 1)
		Goto Ready
	UnZoomAndDeselect:
		MRX7 A 0 A_ZoomFactor(1.0)
		Goto Deselect+1
	DeselectNoCheck:
		Goto Deselect+1
	}
}
